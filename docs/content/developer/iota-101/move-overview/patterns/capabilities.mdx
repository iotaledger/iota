---
description: Introduction to capabilities and their use in authorizing actions within the Move language.
---

# Capabilities

A capability is a design pattern that enables the authorization of specific actions with an object.
For instance,
the [`UpgradeCap`](../package-upgrades/upgrade.mdx#upgrade-requirements) is used to authorize the upgrading of packages.
Another common example is the [`TreasuryCap`](../../../../references/framework/iota-framework/coin.mdx#resource-treasurycap),
which grants the authority to manage treasury functions.

```move
module examples::item {
    use std::string::String;

    /// Type representing the capability to create new `Item`s.
    public struct AdminCap has key { id: UID }

    /// Custom NFT-like type representing an item.
    public struct Item has key, store { id: UID, name: String }

    /// Module initializer, called once during the module's deployment.
    /// This function creates a single instance of `AdminCap` and assigns it to the publisher.
    fun init(ctx: &mut TxContext) {
        transfer::transfer(AdminCap {
            id: object::new(ctx)
        }, tx_context::sender(ctx))
    }

    /// Function to create a new `Item`. It requires `AdminCap` to authorize the action.
    public fun create_item(_: &AdminCap, name: String, ctx: &mut TxContext): Item {
        let item = Item {
            id: object::new(ctx),
            name,
        };
        item
    }
}
```
